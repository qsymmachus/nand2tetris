// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // If (zx == 1) set x = 0
    Mux16(a=x, b[0..15]=false, sel=zx, out=newx);

    // If (nx == 1) set x = !x
    Not16(in=newx, out=notnewx);
    Mux16(a=newx, b=notnewx, sel=nx, out=newestx);

    // If (zy == 1) set y = 0
    Mux16(a=y, b[0..15]=false, sel=zy, out=newy);

    // If (ny == 1) set y = !y
    Not16(in=newy, out=notnewy);
    Mux16(a=newy, b=notnewy, sel=ny, out=newesty);

    // If (f == 1) then out = x + y else out= x & jy
    And16(a=newestx, b=newesty, out=anded);
    Add16(a=newestx, b=newesty, out=added);
    Mux16(a=anded, b=added, sel=f, out=initialout);

    // If (no == 1) then out = !out
    // If (out < 0) set ng = 1 (if first bit is 1, it's negative since we're using 2's complement)
    Not16(in=initialout, out=notout);
    Mux16(a=initialout, b=notout, sel=no, out=out, out[0..7]=out1, out[8..15]=out2, out[15]=ng);

    // If (out == 0) set zr = 1
    Or8Way(in=out1, out=or1); // Check if any non-zero in each half
    Or8Way(in=out2, out=or2);
    Or(a=or1, b=or2, out=notzero); // If either check returned 1, out is not zero
    Not(in=notzero, out=zr);
}

